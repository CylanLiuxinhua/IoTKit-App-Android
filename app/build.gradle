apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.cylan.jiafeigou"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a' //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

    }
    signingConfigs {
        release
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }

    dexOptions {
        jumboMode = true
        javaMaxHeapSize "4g"
        incremental true
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile rootProject.ext.robolectric
    compile rootProject.ext.appcompat
    compile rootProject.ext.butterknife
    compile rootProject.ext.rxAndroid
    compile rootProject.ext.ultra_ptr
    compile rootProject.ext.appSupport
    compile rootProject.ext.recyclerView
    apt rootProject.ext.apt
    compile rootProject.ext.permis
    compile rootProject.ext.systembartint
    apt rootProject.ext.permisApt
    compile project(':jlib')
    compile files('libs/aliyun-oss-sdk-android-2.2.0.jar')
    compile files('libs/gson-2.2.4.jar')
    compile files('libs/mid-sdk-2.3.jar')
    compile files('libs/mta-android-stat-sdk-2.2.0.jar')
    compile files('libs/nineoldandroids-2.4.0.jar')
    compile files('libs/okhttp-3.2.0.jar')
    compile files('libs/okio-1.6.0.jar')
    compile files('libs/open_sdk_r5509_lite.jar')
    compile files('libs/SLog-2.0.jar')
    compile files('libs/weibosdkcore.jar')
    compile files('libs/zxing.jar')
    debugCompile files('src/main/jniLibs/jfg-debug-support.jar')
    releaseCompile files('src/main/jniLibs/jfg-release-support.jar')
    //其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.1.5
    compile rootProject.ext.buglySdk
    //其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.2.0
    compile rootProject.ext.buglyNdk
    compile files('libs/JfgAndroidSDK-2.0.94-sources.jar')
    compile files('libs/JfgAndroidSDK-2.0.94.jar')
}

if (project.hasProperty('keystoreFile') &&
        project.hasProperty('keystorePassword') &&
        project.hasProperty('keystoreAliasPassword')) {
    android.signingConfigs.release.storeFile = file(keystoreFile)
    android.signingConfigs.release.storePassword = keystorePassword
    android.signingConfigs.release.keyPassword = keystoreAliasPassword
    android.signingConfigs.release.keyAlias = keystoreAlias
} else {
    android.buildTypes.release.signingConfig = null
}


if (project.hasProperty('oem')
        && oem != 'cylan') {
    android.defaultConfig.applicationId = 'com.cylan.jiafeigou.' + oem
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        output.processManifest.doLast {
            def manifestOutFile = output.processManifest.manifestOutputFile
            def appkey
            // cylan matappkey:Aqc1103156296  zhongxing matappkey:ATQ1JV694AKT
            if (project.hasProperty('oem') && oem != 'cylan') {
                appkey = "ATQ1JV694AKT"
            } else {
                appkey = "Aqc1103156296"
            }

            def newFileContents = manifestOutFile.getText('UTF-8').replace("MATAPPKEY", appkey)
            manifestOutFile.write(newFileContents, 'UTF-8')

        }
    }
}